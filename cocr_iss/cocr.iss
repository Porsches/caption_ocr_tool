; Important! This file should be encoding with UTF-8-BOM
; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
#define guid "{533A06CF-B5E2-499B-8B3A-43CD54C1625B}"
#define SetupVersion "0.0.1.2"
#define MyAppName "Caption OCR Tool"
#define MyAppExeName "runtime\bin\javaw.exe"
#define MyAppParams "-server -Xmx4G -Dfile.encoding=utf-8 -Dcocr.dir=..\..\app -Djava.library.path=..\..\lib -Dprism.targetvram=2G -Dprism.vsync=false -Dprism.scrollcacheopt=true -Djavafx.preloader=com.neo.caption.ocr.AppPreloader -jar ..\..\app\cocr.jar"
#define MyAppWorkingDir "{app}\runtime\bin"
#define MyAppIco "{app}\Caption OCR Tool.ico"

[Setup]
AppId={{#guid}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=.
OutputBaseFilename=cocr_installer
Compression=lzma2/ultra64
SolidCompression=yes
WizardStyle=modern
VersionInfoVersion={#SetupVersion}
SetupIconFile=..\cocr_setup_win\Caption OCR Tool.ico
UninstallDisplayIcon={#MyAppIco}
LicenseFile=..\LICENSE
ArchitecturesInstallIn64BitMode="x64"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"; InfoBeforeFile: "InfoBefore.txt"
Name: "chinesesimplified"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"; InfoBeforeFile: "InfoBefore-chs.txt"
Name: "chinesetraditional"; MessagesFile: "compiler:Languages\ChineseTraditional.isl"; InfoBeforeFile: "InfoBefore-cht.txt"

[CustomMessages]
UnicodeDir=The installing directory cannot use non-ASCII encoded characters.
english.UnicodeDir=The installing directory cannot use non-ASCII encoded characters.
chinesesimplified.UnicodeDir=安装目录不能使用非 ASCII 编码的字符.
chinesetraditional.UnicodeDir=安裝目錄不能使用非 ASCII 編碼的字元.

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "..\cocr_setup_win\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\lib\*.dll"; DestDir: "{app}\lib"; Flags: ignoreversion
Source: "..\target\cocr.jar"; DestDir: "{app}\app"; Flags: ignoreversion
Source: "..\LICENSE"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Parameters: "{#MyAppParams}"; WorkingDir: "{#MyAppWorkingDir}"; IconFilename: "{#MyAppIco}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; Parameters: "{#MyAppParams}"; WorkingDir: "{#MyAppWorkingDir}"; IconFilename: "{#MyAppIco}"

[Code]
function verifyPath(file: String): boolean;
var
  value: Integer;
  len: Integer;
  i: Integer;
begin
  Result := true;
  len := length(file);
for i := 1 to len do
  begin
  value := ord(file[i]);
  if (value < 32) or (value > 127) then
    begin
      Result := false;
    end;
  end;
end;

function NextButtonClick(CurPageID: Integer): boolean;
begin
  Result := True;
  if (CurPageID = wpSelectDir) then
    begin
    if(verifyPath(WizardDirValue) = false) then
      begin
      Result := False;
      MsgBox(ExpandConstant('{cm:UnicodeDir}'), mbError, MB_OK);
      end;
    end;
end;

function InitializeSetup(): boolean;
var
  ResultStr: String;
  ResultCode: Integer;
begin
  if RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{#guid}_is1', 'UninstallString', ResultStr) then
    begin
      ResultStr := RemoveQuotes(ResultStr);
      Exec(ResultStr, '/silent', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
    end;
    result := true;
end;